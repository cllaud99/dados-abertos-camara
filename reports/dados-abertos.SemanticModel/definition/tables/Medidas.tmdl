table Medidas
	lineageTag: 229ef1b3-f370-49c1-8534-71260fa10b92

	measure Despesas =
			
			COALESCE(
			    SUM(fato__despesas[vlr_liquido]),
			    0
			)
		formatString: "R$"\ #,0.00;-"R$"\ #,0.00;"R$"\ #,0.00
		displayFolder: basicas
		lineageTag: 5a0cacaf-4b7f-45f6-b38a-e8244d554c2b

		annotation PBI_FormatHint = {"currencyCulture":"pt-BR"}

	measure Deputados =
			
			DISTINCTCOUNT(fato__despesas[id_deputado])
		formatString: 0
		displayFolder: basicas
		lineageTag: 8cd32498-c9d9-4d05-b3af-a864f5b0890d

	measure 'Despesas / Deputado' =
			
			DIVIDE( [Despesas] , [Deputados] )
		formatString: "R$"\ #,0.00;-"R$"\ #,0.00;"R$"\ #,0.00
		displayFolder: estatisticas
		lineageTag: 59980e2f-7388-4ee8-b173-da582366872a

		annotation PBI_FormatHint = {"currencyCulture":"pt-BR"}

	measure Partidos = DISTINCTCOUNT( dim__deputados[sigla_partido] )
		formatString: 0
		displayFolder: basicas
		lineageTag: 8534ad06-8658-46ea-93d6-75da00e430b3

	measure '(%) Deputados' = ```
			
			
			VAR _TotalDeputados =
			CALCULATE(
			    COUNTROWS(dim__deputados),
			    ALLSELECTED(dim__deputados)
			)
			
			
			RETURN
			DIVIDE( [Deputados] , _TotalDeputados )
			
			```
		formatString: 0.00%;-0.00%;0.00%
		displayFolder: estatisticas
		lineageTag: c72bf780-fc8e-4fc6-869e-b61332ef2c37

	measure '(%) Despesas' =
			
			
			VAR _Total =
			CALCULATE(
			    SUM(fato__despesas[vlr_liquido]),
			    ALLSELECTED(fato__despesas)
			)
			
			
			RETURN
			DIVIDE( [Despesas] , _Total )
		formatString: 0.00%;-0.00%;0.00%
		displayFolder: estatisticas
		lineageTag: 95a85b91-8641-47d0-93bc-f4b0f80fbb92

	measure Escolaridade = 0
		displayFolder: DataViz
		lineageTag: 5747df3c-5f80-47d7-bcf7-38cf2b37034a

		formatStringDefinition =
				VAR _nome = SELECTEDVALUE(dim__deputados[escolaridade])
				
				RETURN
				"""" & _nome &  """"

	measure Faltante =
			
			1 - [(%) Deputados]
		displayFolder: DataViz
		lineageTag: 7558ec01-f7f3-434f-ac90-4aa51877e505

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'LM Despesas' =
			
			CALCULATE(
			    [Despesas],
			    DATEADD(dim__calendario[date], -1, MONTH )
			)
		displayFolder: estatisticas
		lineageTag: 81920625-66ec-47fe-a6d7-efc4e3fc1ed0

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure Posição =
			
			IF(
			    ISINSCOPE(dim__deputados[nome]),
			    RANKX(
			        ALL(dim__deputados),
			        [Despesas],
			        ,
			        DESC
			    ),
			    BLANK()
			)
		formatString: 0
		displayFolder: DataViz
		lineageTag: 19388796-1812-477c-8582-67da08595374

	measure TituloGraficoIdade =
			
			"Deputados por Idade ㅤㅤㅤㅤㅤ " & "( média - " & [Media idade] & " anos )"
		displayFolder: DataViz
		lineageTag: abc57f8e-6709-43ce-9271-194f048bbc2a

	measure 'Media idade' =
			
			INT( AVERAGE( dim__deputados[idade] ) )
		displayFolder: estatisticas
		lineageTag: 0e99c174-6f7d-4f80-9ed1-b69d98f9c7ac

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure EixoMediaIdade = ```
			
			MAXX( 
			    VALUES( dim__deputados[faixa_idade] ),
			    [Deputados]
			) * 1.30
			```
		displayFolder: DataViz
		lineageTag: 1f01cc17-6fc3-4673-a4da-6c5195aa1845

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure EixoMediaDespesasDeputadosUf = ```
			
			MAXX( 
			    VALUES( dim__deputados[uf] ),
			    MAX( [Despesas / Deputado] , [Despesas Medianas por Deputado] )
			) * 1.20
			```
		displayFolder: DataViz
		lineageTag: 0ae3016a-6b3d-4aa5-805a-55743c4ab6c2

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Despesas Medianas por Deputado' = ```
			
			VAR despesasPorDeputado = 
			    SUMMARIZE(
			        'fato__despesas',
			        'fato__despesas'[id_deputado],
			        "DespesaTotal", SUM('fato__despesas'[vlr_liquido])
			    )
			RETURN
			MEDIANX(despesasPorDeputado, [DespesaTotal])
			```
		displayFolder: estatisticas
		lineageTag: e4f85c01-7bd5-4259-b3d6-22d876c3f4aa

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Despesas Deputado Float' = [Despesas]
		displayFolder: DataViz
		lineageTag: e423e8cf-3c79-4f8b-843a-e22e2ffa5e1d

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'MoM Despesas' =
			
			DIVIDE( [Despesas] - [LM Despesas] , [LM Despesas] )
		displayFolder: estatisticas
		lineageTag: 5c5bbe22-f8ff-4fcd-b542-3e55b95d6673

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure EixoDespesasDeputadosPeriodo = ```
			
			MAXX( 
			    ALLSELECTED(  dim__calendario[Ano], dim__calendario[Mês Abreviado] ) , [Despesas]
			) * 1.3
			```
		displayFolder: DataViz
		lineageTag: 612624d4-7341-4a92-8fc8-6cd42e43ebf9

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Despesas Dept Float' = [Despesas / Deputado]
		displayFolder: DataViz
		lineageTag: c6a2850e-a24f-4979-a644-1227e221f37e

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Rótulo MoM' = [Despesas]
		displayFolder: DataViz
		lineageTag: f25ab514-02c3-474d-983d-c925e0208fce

		formatStringDefinition =
				VAR _num = [MoM Despesas]
				
				VAR _icon = SWITCH(
				     TRUE(),
				     _num > 0, "▲",
				     _num < 0, "▼",
				     "●"
				)
				VAR _num_format = IF(_num < 0, FORMAT(ABS(_num),"0%"), FORMAT(_num,"0%"))
				
				RETURN
				"""" &  _icon & " " & _num_format & """"

	column 'Coluna 1'
		dataType: string
		isHidden
		lineageTag: 69bf4b61-6054-4664-8544-f736ebaaa5f3
		summarizeBy: none
		sourceColumn: Coluna 1

		changedProperty = IsHidden

		annotation SummarizationSetBy = Automatic

	partition Medidas-63ff680f-fab3-4499-9d8e-d63ef7bdb292 = m
		mode: import
		source =
				let
				    Fonte = Table.FromRows(Json.Document(Binary.Decompress(Binary.FromText("i44FAA==", BinaryEncoding.Base64), Compression.Deflate)), let _t = ((type nullable text) meta [Serialized.Text = true]) in type table [#"Coluna 1" = _t])
				in
				    Fonte

	annotation PBI_ResultType = Table

